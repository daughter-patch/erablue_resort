@起床前メニュー上部分表示文章()
#FUNCTIONS

IF 起床前メニュー上部分表示フラグ
	RETURNF TIPS表示文章()
ELSE
	RETURNF かんたんチュートリアル表示文章()
ENDIF


@起床前メニュー上部分文章切り替え(入力番号)
#DIM 入力番号

IF 起床前メニュー上部分表示フラグ
	SELECTCASE 入力番号
		CASE 800
			起床前メニュー上部分表示番号 --
			SIF 起床前メニュー上部分表示番号 <= 0
				起床前メニュー上部分表示番号 = DT_ROW_LENGTH("TIPSデータベース")
		CASE 801
			起床前メニュー上部分表示番号 ++
			SIF 起床前メニュー上部分表示番号 > DT_ROW_LENGTH("TIPSデータベース")
				起床前メニュー上部分表示番号 = 1
		CASE 810 
			起床前メニュー上部分表示番号 = 0
			起床前メニュー上部分表示フラグ = 0
		CASE 811
			起床前メニュー上部分表示番号 = 0
	ENDSELECT
ELSE
	SELECTCASE 入力番号
		CASE 800 TO 802
			起床前メニュー上部分表示番号 = 入力番号
		CASE 809
			起床前メニュー上部分表示番号 = 0
			起床前メニュー上部分表示フラグ = 1
		CASE 810 
			起床前メニュー上部分表示番号 = 0
	ENDSELECT
ENDIF

@かんたんチュートリアル表示文章()
#FUNCTIONS
#DIMS 表示文章

;長さの目安：最初の全角空白と最後の<br>を除いて114まで

表示文章 '= ""
SELECTCASE 起床前メニュー上部分表示番号
	CASE 0
		表示文章 += "■かんたんチュートリアル<br>"
		表示文章 += "　簡易的なチュートリアルが表示されます。項目を選んでください。<br>"
		表示文章 += "<br>"
		表示文章 += "　<button value='800'>[800] まず何すりゃいいの？</button><br>"
		表示文章 += "　<button value='801'>[801] さっさとスケベしたいんだけど</button><br>"
		表示文章 += "　<button value='802'>[802] このキャラに口上付いてる？</button><br>"
		表示文章 += "<br>"
		表示文章 += "　<button value='809'>[809] かんたんチュートリアルを表示しない</button><br>"
	CASE 800
		表示文章 += "■まず何すりゃいいの？<br>"
		表示文章 += "　最初に[110] - リゾートの広告を出す を選び、[1]通常の広告を打つ を選んでください。<br>"
		表示文章 += "　広告を打つことで初めてキャラがリゾートに訪れるようになります。<br>"
		表示文章 += "　その後は[100] - 起床 を選択することでリゾート内に移行し、キャラたちと触れ合うことが出来ます。<br>"
		表示文章 += "<br>"
		表示文章 += "<br>"
		表示文章 += "<br>"
		表示文章 += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
	CASE 801
		表示文章 += "■さっさとスケベしたいんだけど<br>"
		表示文章 += "　このバリアントは、セックスまでが少々遠いバリアントになっています。<br>"
		表示文章 += "　最も簡単な方法は[121] - 能力表示 から、あるいはキャラと同室した状態で能力表示を選び、<br>"
		表示文章 += "　[110]恋慕対象に選ぶ を選択。それから行動を共にすることで恋慕レベルを上げ、告白する[352] を成功させることです。<br>"
		表示文章 += "　告白成功後に二人きりになれば、押し倒す[350] でセックスに持ち込むことが出来ます。<br>"
		表示文章 += "<br>"
		表示文章 += "<br>"
		表示文章 += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
	CASE 802
		表示文章 += "■このキャラに口上付いてる？<br>"
		表示文章 += "　[121] - 能力表示 から[2000]フィルタ機能 を選び、[口上なし] を非表示にすると<br>"
		表示文章 += "　口上ファイルがあるキャラのみ一覧で表示されます。<br>"
		表示文章 += "　ただしこれはファイルがあるかどうかしか見ていないので、中身を保証するものではありません。<br>"
		表示文章 += "　口上の充実度に関してはERBファイルを直接閲覧してください。<br>"
		表示文章 += "<br>"
		表示文章 += "<br>"
		表示文章 += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
ENDSELECT
RETURNF 表示文章


@TIPS表示文章
#FUNCTIONS
#DIMS 表示文章
#DIM NEWフラグ
#DIMS タイトル受渡し
#DIMS 文章受渡し, 6
#DIM ループ用

タイトル受渡し '= ""
VARSET 文章受渡し

SIF 起床前メニュー上部分表示番号 == 0
	起床前メニュー上部分表示番号 = TIPSランダム選出() + 1
NEWフラグ = TIPS文章取得(起床前メニュー上部分表示番号 - 1, タイトル受渡し, 文章受渡し)

表示文章 '= "■TIPS"
IF NEWフラグ
	表示文章 += "<font color='#FF5555'> NEW!!</font>"
ELSE
	表示文章 += "!"
ENDIF
SIF タイトル受渡し != ""
	表示文章 += @"　～%タイトル受渡し%～"
表示文章 += "<br>"

FOR ループ用, 0, 6
	IF 文章受渡し:ループ用 != ""
		表示文章 += @"　%文章受渡し:ループ用%<br>"
	ELSE
		表示文章 += "<br>"
	ENDIF
NEXT
表示文章 += "　<button value='810'>[810] かんたんチュートリアルを表示する</button>　　<button value='811'>[811] TIPSリロール</button>　　<button value='800'>[800] 前へ</button>　<button value='801'>[801] 次へ</button><br>"

RETURNF 表示文章
