@アビテンプレート_回復処理(回復強行フラグ = 0)
#DIM 回復強行フラグ
#DIM 対象番号
#DIM 回復量計算値
#DIM 回復実行フラグ
#DIM 現在ＨＰ保存
#DIM 最大ＨＰ保存
#DIM アンデッドフラグ
#DIM アンデッド行数
#DIM 配列番号

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

IF 戦闘行動キャラ < 10
	回復実行フラグ = 回復強行フラグ
ELSE
	;敵が使用する回復は全員HPMAXでも戻らないのでフラグ立てておく
	回復実行フラグ = 1
ENDIF

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号
	IF 戦闘行動対象 < 10
		;味方対象
		現在ＨＰ保存 = BATTLE_STATE:戦闘行動対象:ＨＰ
		最大ＨＰ保存 = BATTLE_STATE:戦闘行動対象:最大ＨＰ
	ELSE
		;敵対象
		現在ＨＰ保存 = 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ
		最大ＨＰ保存 = 敵BATTLE_STATE:(戦闘行動対象 - 10):最大ＨＰ
	ENDIF
	SIF アビ汎用変数:蘇生可能オプション == 0 && 現在ＨＰ保存 < 1
		CONTINUE
	IF 最大ＨＰ保存 <= 現在ＨＰ保存 && !回復強行フラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のＨＰは既に最大だ
		CONTINUE
	ENDIF

	回復実行フラグ = 1
	CALL 回復量計算処理(アビ汎用変数:効果割合, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)
	回復量計算値 = RESULT + アビ汎用変数:効果量

	;アンデッド判定
	アンデッドフラグ = 0
	アンデッド行数 = バフ・デバフ効果検索("デバフ", "能力", "アンデッド", 戦闘行動対象)
	IF アンデッド行数 > -1
		アンデッド行数 = バフ・デバフ効果検索("デバフ", "能力", "呪い", 戦闘行動対象)
	ENDIF
	IF アビ汎用変数:阻害無視オプション
	ELSEIF アンデッド行数 >= 0
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %DT_CELL_GETS("戦闘効果データベース", アンデッド行数, "効果名")%の効果で回復がダメージに転換される！
		回復量計算値 = 回復量計算値 * DT_CELL_GET("戦闘効果データベース", アンデッド行数, "効果量_割合") / 100
		SIF DT_CELL_GET("戦闘効果データベース", アンデッド行数, "効果量_固定値") > 0
			回復量計算値 = MIN(回復量計算値, DT_CELL_GET("戦闘効果データベース", アンデッド行数, "効果量_固定値"))
		アンデッドフラグ = 1
		CALL デバフ回数経過処理(戦闘行動対象, "アンデッド")
		CALL デバフ回数経過処理(戦闘行動対象, "呪い")
	ELSEIF 展開中フィールド名格納 != ""
		RESULTS '= ""
		TRYCCALLFORM 固有アンデッド補正_フィールド_%展開中フィールド名格納%(回復量計算値)
			IF RESULTS == ""
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %展開中フィールド名格納%が回復をダメージに転換する！
			ELSE
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %RESULTS%
			ENDIF
			SIF RESULT > 0
				回復量計算値 = RESULT
			アンデッドフラグ = 1
		CATCH
		ENDCATCH
	ENDIF
	IF アンデッドフラグ
		IF 現在ＨＰ保存 < 1
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%は復活しなかった…
			CONTINUE
		ENDIF
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%に{回復量計算値}のダメージ！
		IF 戦闘行動対象 < 10
			;味方対象
			BATTLE_STATE:戦闘行動対象:ＨＰ = MAX(BATTLE_STATE:戦闘行動対象:ＨＰ - 回復量計算値, 0)
			現在ＨＰ保存 = BATTLE_STATE:戦闘行動対象:ＨＰ
		ELSE
			;敵対象
			敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ = MAX(敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ - 回復量計算値, 0)
			現在ＨＰ保存 = 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ
		ENDIF
		IF 現在ＨＰ保存 < 1
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%は倒れた！
			CALL 戦闘不能者登録(戦闘行動対象, -1)
		ENDIF
		CALL ダメージ表示アニメ(回復量計算値, 戦闘行動対象, 0, 0, 0)
	ELSE
		回復量計算値 = MAX(MIN(回復量計算値, 最大ＨＰ保存 - 現在ＨＰ保存), 0)
		IF バフ・デバフ効果検索("デバフ", "能力", "強圧", 戦闘行動対象) >= 0 && !アビ汎用変数:阻害無視オプション
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のＨＰは回復しなかった…
			回復量計算値 = 0
			CALL デバフ回数経過処理(戦闘行動対象, "強圧")
		ELSE
			IF 現在ＨＰ保存 < 1
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%が復活した！
				戦闘中カウント:戦闘行動対象:死亡時ダメ属性 = 0
				IF 戦闘行動キャラ < 10 && 戦闘行動対象 < 10
					;味方＞味方の回復の場合、口上用変数を記録
					CALL 反応口上記録処理("被蘇生時")
				ENDIF
			ENDIF
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のＨＰが{回復量計算値}回復した！
			IF 戦闘行動対象 < 10
				;味方対象
				BATTLE_STATE:戦闘行動対象:ＨＰ += 回復量計算値
				IF 戦闘行動キャラ < 10
					;味方＞味方の回復の場合、口上用変数を記録
					CALL 反応口上記録処理("被回復時")
				ENDIF
			ELSE
				;敵対象
				敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ += 回復量計算値
			ENDIF
			;特殊反応中は連鎖させない
			IF 特殊反応フラグ == 0 && 回復量計算値 > 0
				;被回復登録
				IF MATCH(被回復反応者, 戦闘行動対象) < 1
					配列番号 = FINDELEMENT(被回復反応者, -1)
					被回復反応者:配列番号 = 戦闘行動対象
					被回復反応対象:配列番号 = 戦闘行動キャラ
					被回復時最大量:配列番号 = 回復量計算値
					被回復時総量:配列番号 = 回復量計算値
				ELSE
					配列番号 = FINDELEMENT(被回復反応者, 戦闘行動対象)
					被回復時総量:配列番号 += 回復量計算値
					SIF 被回復時最大量:配列番号 < 回復量計算値
						被回復時最大量:配列番号 = 回復量計算値
				ENDIF
			ENDIF
		ENDIF
		CALL ダメージ表示アニメ(回復量計算値, 戦闘行動対象, 0, 1, 0)
	ENDIF
NEXT

IF 回復実行フラグ == 0
	;誰も回復者がいないならキャンセルして戻る
	CALL 口上変数初期化
	WSTR:(K++) = 対象のＨＰは既に最大です
	CALL メッセージ欄表示用関数(,,,0)
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF

CALL 全滅チェック


@アビテンプレート_回復処理_MAXでも回復
JUMP アビテンプレート_回復処理(1)

@アビテンプレート_ＭＰ回復処理(回復強行フラグ = 0)
#DIM 回復強行フラグ
#DIM 対象番号
#DIM 回復量計算値
#DIM 回復実行フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

IF 戦闘行動キャラ < 10
	回復実行フラグ = 回復強行フラグ
ELSE
	;敵が使用する回復は全員MPMAXでも戻らないのでフラグ立てておく
	回復実行フラグ = 1
ENDIF

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	;敵対象はＭＰ無いので何もしない
	SIF アビテンプレート用_対象保存:対象番号 >= 10
		CONTINUE
	;残りは味方対象
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号
	IF BATTLE_STATE:戦闘行動対象:最大ＭＰ <= BATTLE_STATE:戦闘行動対象:ＭＰ && !回復強行フラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%のＭＰは既に最大だ
		CONTINUE
	ENDIF

	回復実行フラグ = 1
	CALL 回復量計算処理(アビ汎用変数:効果割合, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)
	回復量計算値 = RESULT + アビ汎用変数:効果量
	回復量計算値 = MAX(MIN(回復量計算値, BATTLE_STATE:戦闘行動対象:最大ＭＰ - BATTLE_STATE:戦闘行動対象:ＭＰ), 0)

	アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%のＭＰが{回復量計算値}回復した！
	BATTLE_STATE:戦闘行動対象:ＭＰ = BATTLE_STATE:戦闘行動対象:ＭＰ + 回復量計算値
	IF 戦闘行動キャラ < 10
		;味方＞味方の回復の場合、口上用変数を記録
		CALL 反応口上記録処理("被ＭＰ回復時")
	ENDIF
NEXT

IF 回復実行フラグ == 0
	;誰も回復者がいないならキャンセルして戻る
	CALL 口上変数初期化
	WSTR:(K++) = 対象のＭＰは既に最大です
	CALL メッセージ欄表示用関数(,,,0)
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF

@アビテンプレート_ＭＰ回復処理_MAXでも回復
JUMP アビテンプレート_ＭＰ回復処理(1)
